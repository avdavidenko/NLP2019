# Определение согласных и гласных (и их ряда).
define Consonant b | c | d | f | g | h | j | k | l | m | n | p | q | r | s | t | v | w | x | z ;
define FrontVowel ä | ö | y ;
define NeutralVowel e | i ;
define BackVowel a | o | u ;
define Vowel FrontVowel | NeutralVowel | BackVowel | A | E;
define Letter Vowel | Consonant;

# Определение тегов для падежей и чисел.
define Case "+Nom" | "+Gen" | "+Part" | "+In" | "+All" | "+El" | "+Ad" | "+Abl" | "+Ill" ;
define Number "+Sg" | "+Pl" ; 

# Определение типов основ.
define FrontStem [Consonant* [ FrontVowel | NeutralVowel ] ]+ Consonant*;
define BackStem [Consonant* [ BackVowel | NeutralVowel ] ]* [Consonant* BackVowel] [Consonant* [ BackVowel | NeutralVowel ] ]* Consonant*;
define Stem Letter+;

define Input Stem Number Case;

# Обработка основ на согласную. Что делать с окончаниями, программа, очевидно, не знает, но основы вроде бы образует правильно.
# Обработка основ типа ihminen.
define IhminenPl n e n -> s || _ "+Pl" ;
define IhminenPart n e n -> s || _ "+Sg" "+Part" ;
define IhminenCase n e n -> s e || _ "+Sg" ;
define Ihminen IhminenPl .o. IhminenPart .o. IhminenCase ;

# Обработка основ типа ajatus.
define AjatusPl s -> k s || Consonant Vowel _ "+Pl" ;
define AjatusPart s -> s || Consonant Vowel _ "+Sg" "+Part" ;
define AjatusCase s -> k s e || Consonant Vowel _ "+Sg" ;
define Ajatus AjatusPl .o. AjatusPart .o. AjatusCase ;

# Обработка основ типа hyvyys.
define HyvyysPl s -> k s || Consonant Vowel Vowel _ "+Pl" ;
define HyvyysPart s -> t || Consonant Vowel Vowel _ "+Sg" "+Part" ;
define HyvyysCase s -> d e || Consonant Vowel Vowel _ "+Sg" ;
define Hyvyys HyvyysPl .o. HyvyysPart .o. HyvyysCase ;

# Обработка основ типа askel.
define AskelLPl l -> l || e _ "+Pl" ;
define AskelLPart l -> l || e _ "+Sg" "+Part" ;
define AskelLCase l -> l e || e _ "+Sg" ;
define AskelNPl n -> n || e _ "+Pl" ;
define AskelNPart n -> n || e _ "+Sg" "+Part" ;
define AskelNCase n -> n e || e _ "+Sg" ;
define Askel AskelLCase .o. AskelLPl .o. AskelLPart .o. AskelNCase .o. AskelNPl .o. AskelNPart ;

# Обработка основ типа lyhyt.
define LyhytPl t -> 0 || [ y | u ] _ "+Pl" ;
define LyhytPart t -> t "^" || [ y | u ] _ "+Sg" "+Part" ;
define LyhytCase t -> e || [ y | u ] _ "+Sg" ;
define Lyhyt LyhytPl .o. LyhytPart .o. LyhytCase ;

# В номинативе при множественном числе к основе добавляется -t, при единственном окончание нулевое.
define NomPl "+Pl" "+Nom" -> "^" t || _ ;
define NomSg "+Sg" "+Nom" -> "^" || _ ;
define Nominatives NomPl .o. NomSg ;

# В генетиве во множественном числе добавляется -den, если основа заканчивается на две гласные (дифтонг или долгую гласную). Если нет - добавляется -en.
define GenDen "+Gen" -> d e n || Vowel Vowel "+Pl" _ ;
define Gen "+Gen" -> E n || _ ;
define Genetive GenDen .o. Gen ;

# В партитиве добавляется -tA, если основа заканчивается на согласная-гласная-гласная (то есть дифтонги и долгие гласные) или на согласную. В остальных случаях используется -A.
define PartTa "+Part" -> t A || [ Consonant Vowel Vowel | Consonant ] Number _ ;
define PartA "+Part" -> A || _ ;

# Локативы. Различие между -in и -hin во множественном числе не работает в полной мере, потому что для него надо обрабатывать основы. Например, делать для lehti основу leht. В файле .in для проверки есть и полное слово, и голая основа.
define LocativeCases "+In" -> s s A , "+El" -> s t A , "+Ad" -> l l A , "+Abl" -> l t A , "+All" -> l l A || _ ;
define IllativeSgO "+Sg" "+Ill" -> o n || o _ ;
define IllativeSgU "+Sg" "+Ill" -> u n || u _ ;
define IllativeSgA "+Sg" "+Ill" -> a n || a _ ;
define IllativeSgE "+Sg" "+Ill" -> e n || e _ ;
define IllativeSgI "+Sg" "+Ill" -> i n || i _ ;
define IllativeSgAA "+Sg" "+Ill" -> ä n || ä _ ;
define IllativeSgOO "+Sg" "+Ill" -> ö n || ö _ ;
define IllativeSgY "+Sg" "+Ill" -> y n || y _ ;
define IllativePlHin "+Ill" -> h i n || Vowel "+Pl" _ ;
define IllativePlIn "+Ill" -> i n || Consonant "+Pl" _ ;

define IllativeSg IllativeSgA .o. IllativeSgAA .o. IllativeSgE .o. IllativeSgI .o. IllativeSgO .o. IllativeSgOO .o. IllativeSgU .o. IllativeSgY ;
define Illative IllativeSg .o. IllativePlHin .o. IllativePlIn ;

define OtherCases PartTa .o. PartA .o. LocativeCases .o. Illative ;

# В единственном числе показатель числа не добавляется. ä и i на конце основы опускаются. Дальше условия про множественное число. В двусложных основах конечное a меняется на o, если первая гласная основы - a, e или i. Если первая гласная o или u, то a просто опускается. Гласная перед показателем множественного числа опускается, если после множественного числа идет согласная. Для указания на множественное число используется -i.
define Sg "+Sg" -> "^" || _ ;
define OmitA ä -> 0, i -> 0 || _ "+Pl";
define Ai2Oi a -> o || [ a | e | i ] Vowel* Consonant+ _ "+Pl";
define Ai2i a -> 0 || [ o | u ] Vowel* Consonant+ _ "+Pl";
define OmitFirst Vowel -> 0 || _ Vowel "+Pl" Consonant;
define Pl "+Pl" -> "^" i || _ ;

define NumberInfl Sg .o. OmitA .o. Ai2i .o. Ai2Oi .o. OmitFirst .o. Pl ;

# Во множественном числе i между гласными заменяется на j. С основами заднего ряда употребляется форма с заднерядными гласными, с основами переднего - с передними (соответственно А заменяется либо на a, либо на ä).
define JotPl i -> j || Vowel "^"* _ "^"* Vowel ;
define HarmonyBack A -> a || BackStem "^" Letter* _;
define HarmonyFront A -> ä || FrontStem "^" Letter* _;

define Phonetics JotPl .o. HarmonyBack .o. HarmonyFront ;

# Описание чередований согласных в основах, если слог основы закрывается. Потом чередования, происходящие перед гласным основы. Последние два строки - исчезновение E только после гласной.
define DoublePTK p p -> p , t t -> t , k k -> k || Letter _ "^"* Vowel "^"* Consonant [ .#. | Consonant ] ;
define SingleP p -> v || Vowel _ "^"* Vowel "^"* Consonant [ .#. | Consonant ] ;
define SingleTK t -> d , k -> 0 || Letter _ "^"* Vowel "^"* Consonant [ .#. | Consonant ] ; 
define TwoLetter m p -> m m , n t -> n n , n k -> n g , l t -> l l , r t -> r r || Letter _ "^"* Vowel "^"* Consonant [ .#. | Consonant ] ;
define ThreeLetter l k e -> l j e , r k e -> r j e , h k e -> h j e , u k u -> u v u || Letter _ "^"* Consonant [ .#. | Consonant ] ;
define N2M i n -> i m , t ö n -> t t ö m || _ "^" Vowel;
define EOmit E -> 0 || Vowel "^" _;
define EReal E -> e || _;

define Alternations N2M  .o. EOmit .o. EReal .o. ThreeLetter .o. TwoLetter .o. SingleP .o. SingleTK .o. DoublePTK ;

# Описание, в каком порядке применять преобразования.
define Affixes Hyvyys .o. Ajatus .o. Ihminen .o. Askel .o. Nominatives .o. Genetive .o. OtherCases .o. NumberInfl .o. Alternations .o. Phonetics;

# Чистка от знаков ^.
define Cleanup "^" -> 0 || _ ;

# Описание, в каком порядке применять функции.
define Grammar Input .o. Affixes .o. Cleanup ;
push Grammar